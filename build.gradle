plugins {
    id 'com.github.sherter.google-java-format' version '0.8'
}

// Apply the java-library plugin to add support for Java Library
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
//apply plugin: 'war'
apply plugin: 'jacoco'

sourceCompatibility = '1.8'
version = '1.0.0-SNAPSHOT'

task sourcesJar(type: Jar) {
	from 'dist/stub/stub.jar'
	classifier = 'sources'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.webank'
			artifactId = 'wecross-java-stub'
			from components.java
			artifact sourcesJar

			// https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
			pom {
				name = "WeCross-Stub"
				description = "Stub interface for WeCross"
				url = "https://github.com/WeBankFinTech/WeCross"
				licenses {
					license {
						name = "The Apache License, Version 2.0"
						url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
					}
				}
				developers {
					developer {
						id = "Shareong"
						name = "Shareong"
						email = "740310627@qq.com"
					}
				}
				scm {
					connection = "scm:git:git://github.com/WeBankFinTech/WeCross.git"
					developerConnection = "scm:git:ssh://github.com/WeBankFinTech/WeCross.git"
					url = "https://github.com/WeBankFinTech/WeCross"
				}
			}
		}
	}
	repositories {
		maven {
			def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username = NEXUS_USERNAME
				password = NEXUS_PASSWORD
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url 'https://dl.bintray.com/ethereum/maven/' }
}

googleJavaFormat {
    options style: 'AOSP'
    source = sourceSets*.allJava
    include '**/*.java'
    exclude '**/temp/*.java'
}

verifyGoogleJavaFormat {
    source = sourceSets*.allJava
    include '**/*.java'
    exclude '**/temp/*.java'
}

configurations.all {
    exclude module: "spring-boot-starter-logging"
}

dependencies {
    compile('org.quartz-scheduler:quartz:2.3.2')
    compile 'com.moandjiezana.toml:toml4j:0.7.2'
    compile 'org.springframework.boot:spring-boot-starter-log4j2:2.1.4.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-actuator:2.1.4.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-web:2.1.4.RELEASE'
    compile 'org.springframework.boot:spring-boot-configuration-processor:2.1.4.RELEASE'
	compile 'org.rocksdb:rocksdbjni:6.6.4'
	compile 'io.netty:netty-all:4.1.47.Final'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.4.RELEASE'
}

sourceSets {
    main {
        resources {
            exclude '/*'
        }
    }
}

jar {
    destinationDir file('dist/apps')
    archiveName project.name + '.jar'

    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'com.webank.wecross.Application'
        )
    }

    doLast {
        copy {
            from file('src/main/resources/')
            into 'dist/conf'
        }
        copy {
            from configurations.runtime
            into 'dist/lib'
        }
        copy {
            from file('.').listFiles().findAll { File f -> (f.name.endsWith('.sh') || f.name.endsWith('.env')) }
            into 'dist'
        }
        copy {
            from file('scripts/')
            into 'dist'
        }
    }
}

task makeStubJAR(type:org.gradle.api.tasks.bundling.Jar) {
	baseName 'stub'
	from 'build/classes/java/main/com/webank/wecross/stub'
	from 'src/main/java/com/webank/wecross/stub'
	into 'com/webank/wecross/stub/'
	
	destinationDir file('dist/stub/')
}

test {
    testLogging.showStandardStreams = true
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
